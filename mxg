#!/usr/bin/perl
#
# MXGuardian Command Line Interface
# https://www.mxguardian.net
#
#
use strict;
use warnings FATAL => 'all';
use File::Basename;
use Getopt::Long qw(:config require_order);
use Pod::Usage;

=head1 NAME

Command Line Interface to MXGuardian

=head1 VERSION

0.1.0

=head1 SYNOPSIS

mxg [options] <command> [parameters]

To see help text, you can run:

    mxg -h
    mxg <command> -h

Options:

    -h       help
    -v       verbose

=head1 COMMANDS

=cut

my $help = 0;
my $verbose = 0;
GetOptions(
    'help|?' => \$help,
    verbose => \$verbose
) or pod2usage(2);
pod2usage(1) if $help;

my $command = shift;
pod2usage("The following arguments are required: command") unless defined($command);

pod2usage("Missing required environment variable: MXG_API_KEY") unless defined($ENV{MXG_API_KEY});
my $client = MXG::API::Client->new(
    $ENV{MXG_API_ENDPOINT} || 'https://secure.mxguardian.net/api/v1',
    $ENV{MXG_API_KEY}
);

if ( $command eq 'search-inbound' ) {
    search('inbound');
} elsif ( $command eq 'search-outbound' ) {
    search('outbound');
} else {
    pod2usage("Unknown command: $command");
}

=head2 B<search-inbound>

mxg search-inbound [options] [search phrase]

 Options:
   --domain <value> | --user <value>
   [--columns <value>]
   [--page <value]
   [-pagesize <value>]

 Default column names:
   mail_id,date,author,subject

 Additional column names (may be specified with --columns)
   sender,from,score,ip

=head2 B<search-outbound>

See search-inbound for details

=cut

sub search {
    my $mode = shift;
    my $columns = "mail_id,date,author,subject";
    my $page = 1;
    my $pagesize = 100;
    my $help = 0;
    my ($domain,$user);

    GetOptions (
        "help"       => \$help,
        "domain=s"   => \$domain,
        "user=s"     => \$user,
        "columns=s"  => \$columns,
        "page=i"     => \$page,
        "pagesize=i" => \$pagesize,
    ) or do {
        $help = 1;
    };
    pod2usage(
        -sections => "COMMANDS/search",
        -verbose  => 99
    ) if $help;

    my %params = (
        page     => $page,
        pagesize => $pagesize,
    );
    $params{q} = shift(@ARGV) if @ARGV;

    my $path;
    if ( defined($domain) ) {
        $path = "domains/$domain/$mode";
    } elsif ( defined($user) ) {
        $path = "users/$user/$mode";
    } else {
        pod2usage(
            -msg      => "Must specify --domain or --user",
            -sections => [ qw(COMMANDS/search-inbound COMMANDS/search-outbound) ],
            -verbose  => 99
        );
    }

    #@type MXG::API::Response;
    my $response = $client->request(path => $path, params => \%params );
    die $response->status_line()."\n" unless $response->is_success();
    print $response->status_line,"\n" if $verbose;

    my @columns = split(',',$columns);
    for my $msg (@{$response->json()}) {
        for (@columns) {
            if ( $_ eq "mail_id" ) {
                printf '%s ',$msg->{mail_id};
            } elsif ( $_ eq "date" ) {
                printf '%s ',$msg->{date};
            } elsif ( $_ eq "author" ) {
                printf '%-40s ',$msg->{author};
            } elsif ( $_ eq "subject" ) {
                printf '%-50s ',$msg->{subject};
            } elsif ( $_ eq "ip" ) {
                printf '%-25s ',$msg->{ip};
            } elsif ( $_ eq "score" ) {
                printf '%7.3f ',$msg->{score};
            }
        }
        print "\n";
    }
}

package MXG::API::Client;
use strict;
use warnings FATAL => 'all';
use LWP;

sub new {
    my ($class,$endpoint,$api_key) = @_;
    $endpoint =~ s/\/$//; # remove trailing slash from endpoint
    bless {
        endpoint => $endpoint,
        api_key  => $api_key,
        ua       => LWP::UserAgent->new(
            timeout => 10
        )
    }, $class;
}

sub request {
    my $self = shift;
    my %args = (
        method => 'GET',
        path => '',
        params => {},
        headers => {},
        data => '',
        @_
    );

    $args{path} =~ s/^\///;  # remove leading slash from path

    my $uri = URI->new($self->{endpoint}.'/'.$args{path} );
    $uri->query_form($args{params});

    my %headers = %{$args{headers}};

    $headers{Authorization} = 'Bearer '.$self->{api_key};
    $headers{'Accept'} = 'application/json';

    print $uri->as_string,"\n" if $verbose;
    my $request = HTTP::Request->new(
        $args{method},
        $uri->as_string,
        HTTP::Headers->new(%headers),
        $args{data}
    );

    bless $self->{ua}->request($request), 'MXG::API::Response';

}

1;

package MXG::API::Response;
use strict;
use warnings FATAL => 'all';
use JSON;

use parent 'HTTP::Response';

sub json {
    JSON::from_json(shift->content());
}


1;
