#!/usr/bin/perl
#
# MXGuardian Command Line Interface
# https://www.mxguardian.net
#
#
use strict;
use warnings FATAL => 'all';
use File::Basename;
use Getopt::Long qw(:config require_order);
use Pod::Usage;

=head1 NAME

Command Line Interface to MXGuardian

=head1 VERSION

0.1.0

=head1 SYNOPSIS

mxg [options] <command> [parameters]

To see help text, you can run:

    mxg -h
    mxg <command> -h

Options:

    -h       help
    -v       verbose

=head1 COMMANDS

=cut

my $_help = 0;
my $verbose = 0;
GetOptions(
    'help|?' => \$_help,
    verbose => \$verbose
) or pod2usage(2);
pod2usage(1) if $_help;

my $command = shift;
pod2usage("The following arguments are required: command") unless defined($command);

pod2usage("Missing required environment variable: MXG_API_KEY") unless defined($ENV{MXG_API_KEY});
my $client = MXG::API::Client->new(
    $ENV{MXG_API_ENDPOINT} || 'https://secure.mxguardian.net/api/v1',
    $ENV{MXG_API_KEY}
);

if ( $command eq 'search-inbound' ) {
    search('inbound');
} elsif ( $command eq 'search-outbound' ) {
    search('outbound');
} elsif ( $command eq 'list-users' ) {
    list_users();
} elsif ( $command eq 'add-user' ) {
    add_user();
} elsif ( $command eq 'remove-user' ) {
    remove_user();
} else {
    pod2usage("Unknown command: $command");
}

=head2 B<search-inbound>

mxg search-inbound [options] [search phrase]

 Options:
   --domain <value> | --user <value>
   [--columns <value>]
   [--page <value]
   [-pagesize <value>]

 Default column names:
   mail_id,date,author,subject

 Additional column names (may be specified with --columns)
   sender,from,score,ip

=head2 B<search-outbound>

See search-inbound for details

=cut

sub search {
    my $mode = shift;
    my $columns = "mail_id,date,author,subject";
    my $page = 1;
    my $pagesize = 100;
    my $help = 0;
    my ($domain,$user);

    GetOptions (
        "help"       => \$help,
        "domain=s"   => \$domain,
        "user=s"     => \$user,
        "columns=s"  => \$columns,
        "page=i"     => \$page,
        "pagesize=i" => \$pagesize,
    ) or do {
        $help = 1;
    };
    pod2usage(
        -sections => "COMMANDS/search",
        -verbose  => 99
    ) if $help;

    my %params = (
        page     => $page,
        pagesize => $pagesize,
    );
    $params{q} = shift(@ARGV) if @ARGV;

    my $path;
    if ( defined($domain) ) {
        $path = "domains/$domain/$mode";
    } elsif ( defined($user) ) {
        $path = "users/$user/$mode";
    } else {
        pod2usage(
            -msg      => "Must specify --domain or --user",
            -sections => [ qw(COMMANDS/search-inbound COMMANDS/search-outbound) ],
            -verbose  => 99
        );
    }

    my $response = $client->request(path => $path, params => \%params );
    print $response->status() if $verbose;

    my @columns = split(',',$columns);
    for my $msg (@{$response->json()}) {
        for (@columns) {
            if ( $_ eq "mail_id" ) {
                printf '%s ',$msg->{mail_id};
            } elsif ( $_ eq "date" ) {
                printf '%s ',$msg->{date};
            } elsif ( $_ eq "author" ) {
                printf '%-40s ',$msg->{author};
            } elsif ( $_ eq "subject" ) {
                printf '%-50s ',$msg->{subject};
            } elsif ( $_ eq "ip" ) {
                printf '%-25s ',$msg->{ip};
            } elsif ( $_ eq "score" ) {
                printf '%7.3f ',$msg->{score};
            }
        }
        print "\n";
    }
}

=head2 list-users

mxg list-users [options]

 Options:
   --domain <value>
   [--include-aliases]

=cut

sub list_users {
    my $page = 1;
    my $pagesize = 100;
    my $help = 0;
    my $include_aliases = 0;
    my $domain;

    GetOptions (
        "help"       => \$help,
        "domain=s"   => \$domain,
        "include-aliases" => \$include_aliases,
        "page=i"     => \$page,
        "pagesize=i" => \$pagesize,
    ) or do {
        $help = 1;
    };
    pod2usage(
        -sections => "COMMANDS/list-users",
        -verbose  => 99
    ) if $help;
    pod2usage(
        -msg => "Must specify --domain",
        -sections => "COMMANDS/list-users",
        -verbose  => 99
    ) if !defined($domain);

    my $path = "/domains/$domain/users";
    my %params;
    my $response = $client->request(path => $path, params => \%params );
    print $response->status() if $verbose;

    for my $user (@{$response->json()->{results}}) {
        printf("%s\n",$user->{user_email});
        if ( $include_aliases ) {
            for my $alias (@{$user->{user_aliases}}) {
                printf("  (%s)\n",$alias);
            }
        }
    }
}

=head2 add-user

mxg add-user <user_email>

=cut

sub add_user {
    my $help = 0;

    GetOptions (
        "help"       => \$help,
    ) or do {
        $help = 1;
    };
    pod2usage(
        -sections => "COMMANDS/add-user",
        -verbose  => 99
    ) if $help;

    my $user_email = shift(@ARGV);
    pod2usage(
        -msg => "Please specify a valid email address",
        -sections => "COMMANDS/add-user",
        -verbose  => 99
    ) if !defined($user_email) or !MXG::Utils->validateEmail($user_email);

    my $domain = MXG::Utils->getDomainFromEmail($user_email);

    my $path = "/domains/$domain/users";
    my $data = { user_email => $user_email };
    my $response = $client->request(method => 'POST', path => $path, data => $data );
    print $response->status();

}

=head2 remove-user

mxg remove-user <user_email>

=cut

sub remove_user {
    my $help = 0;

    GetOptions (
        "help"       => \$help,
    ) or do {
        $help = 1;
    };
    pod2usage(
        -sections => "COMMANDS/remove-user",
        -verbose  => 99
    ) if $help;

    my $user_email = shift(@ARGV);
    pod2usage(
        -msg => "Please specify a valid email address",
        -sections => "COMMANDS/remove-user",
        -verbose  => 99
    ) if !defined($user_email) or !MXG::Utils->validateEmail($user_email);

    my $path = "/users/$user_email";
    my $response = $client->request(method => 'DELETE', path => $path );
    print $response->status();

}

package MXG::Utils;
use strict;
use warnings FATAL => 'all';

sub getDomainFromEmail {
    my ($self,$email) = @_;
    my ($localpart,$domain) = split('@',$email);
    lc($domain);
}

sub validateEmail {
    my ($self,$email) = @_;
    $email =~ /^([a-zA-Z0-9\+\_\=\.\-])+@([a-zA-Z0-9\-])+(\.[a-zA-Z0-9\-]+)+$/;
}

1;

package MXG::API::Client;
use strict;
use warnings FATAL => 'all';
use LWP;
use JSON;

sub new {
    my ($class,$endpoint,$api_key) = @_;
    $endpoint =~ s/\/$//; # remove trailing slash from endpoint
    bless {
        endpoint => $endpoint,
        api_key  => $api_key,
        ua       => LWP::UserAgent->new(
            timeout => 10
        )
    }, $class;
}

sub request {
    my $self = shift;
    my %args = (
        method => 'GET',
        path => '',
        params => {},
        headers => {},
        data => '',
        @_
    );

    $args{path} =~ s/^\///;  # remove leading slash from path
    $args{data} = JSON::to_json($args{data}) if ref($args{data});

    my $uri = URI->new($self->{endpoint}.'/'.$args{path} );
    $uri->query_form($args{params});

    my %headers = %{$args{headers}};

    $headers{Authorization} = 'Bearer '.$self->{api_key};
    $headers{'Content-Type'} = 'application/json';
    $headers{'Accept'} = 'application/json';

    print $args{method}.' '.$uri->as_string,"\n" if $verbose;
    print $args{data},"\n" if $verbose and $args{data};
    my $request = HTTP::Request->new(
        $args{method},
        $uri->as_string,
        HTTP::Headers->new(%headers),
        $args{data}
    );

    #@type MXG::API::Response
    my $response = bless $self->{ua}->request($request), 'MXG::API::Response';
    die $response->status() unless $response->is_success();

    return $response;
}

1;

package MXG::API::Response;
use strict;
use warnings FATAL => 'all';
use JSON;

use parent 'HTTP::Response';

sub json {
    JSON::from_json(shift->content());
}

sub status {
    shift->status_line."\n";
}


1;
